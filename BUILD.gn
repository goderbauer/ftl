# Copyright 2016 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
}

config("thread_safety_warning") {
  if (is_fuchsia) {
    cflags = [ "-Wthread-safety" ]
  }
}

# These are sources that store static global state that may impose potential
# constraints on dynamic linking. We separate these out here.
source_set("stateful_sources") {
  sources = [
    "log_level.h",
    "log_settings.h",
    "log_settings_state.cc",
  ]
}

static_library("ftl") {
  sources = [
    "arraysize.h",
    "command_line.cc",
    "command_line.h",
    "debug/debugger.cc",
    "debug/debugger.h",
    # TODO(abarth): Move files/* out to its own library.
    "files/directory.cc",
    "files/directory.h",
    "files/eintr_wrapper.h",
    "files/file_descriptor.cc",
    "files/file_descriptor.h",
    "files/file.cc",
    "files/file.h",
    "files/path.cc",
    "files/path.h",
    "files/scoped_temp_dir.cc",
    "files/scoped_temp_dir.h",
    "files/symlink.cc",
    "files/symlink.h",
    "files/unique_fd.cc",
    "files/unique_fd.h",
    "functional/closure.h",
    "functional/make_runnable.h",
    "functional/make_copyable.h",
    "log_settings.cc",
    "logging.cc",
    "logging.h",
    "macros.h",
    "memory/ref_counted.h",
    "memory/ref_counted_internal.h",
    "memory/ref_ptr.h",
    "memory/ref_ptr_internal.h",
    "memory/unique_object.h",
    "memory/weak_ptr.h",
    "memory/weak_ptr_internal.cc",
    "memory/weak_ptr_internal.h",
    "strings/ascii.cc",
    "strings/ascii.h",
    "strings/split_string.cc",
    "strings/split_string.h",
    "strings/string_number_conversions.cc",
    "strings/string_number_conversions.h",
    "strings/string_printf.cc",
    "strings/string_printf.h",
    "strings/string_view.cc",
    "strings/string_view.h",
    "strings/trim.cc",
    "strings/trim.h",
    "strings/utf_codecs.cc",
    "strings/utf_codecs.h",
    "synchronization/cond_var.cc",
    "synchronization/cond_var.h",
    "synchronization/monitor.cc",
    "synchronization/monitor.h",
    "synchronization/mutex.cc",
    "synchronization/mutex.h",
    "synchronization/sleep.cc",
    "synchronization/sleep.h",
    "synchronization/thread_annotations.h",
    "synchronization/thread_checker.h",
    "synchronization/waitable_event.cc",
    "synchronization/waitable_event.h",
    "tasks/one_shot_timer.cc",
    "tasks/one_shot_timer.h",
    "tasks/task_runner.cc",
    "tasks/task_runner.h",
    "time/stopwatch.cc",
    "time/stopwatch.h",
    "time/time_delta.h",
    "time/time_point.cc",
    "time/time_point.h",
    "third_party/icu/icu_utf.h",
    "third_party/icu/icu_utf.cc",
  ]

  if (is_android) {
    defines = [ "ANDROID_LOG_TAG=$android_log_tag" ]
    libs = [ "log" ]
  }

  if (is_fuchsia) {
    libs = [ "magenta" ]
  }

  public_configs = [ ":thread_safety_warning" ]

  public_deps = [
    ":stateful_sources",
  ]
}

executable("ftl_unittests") {
  testonly = true

  sources = [
    "arraysize_unittest.cc",
    "command_line_unittest.cc",
    "log_settings_unittest.cc",
    "files/file_unittest.cc",
    "files/path_unittest.cc",
    "files/scoped_temp_dir_unittest.cc",
    "functional/make_copyable_unittest.cc",
    "memory/ref_counted_unittest.cc",
    "memory/weak_ptr_unittest.cc",
    "tasks/one_shot_timer_unittest.cc",
    "strings/ascii_unittest.cc",
    "strings/split_string_unittest.cc",
    "strings/string_number_conversions_unittest.cc",
    "strings/string_printf_unittest.cc",
    "strings/string_view_unittest.cc",
    "strings/trim_unittest.cc",
    "synchronization/cond_var_unittest.cc",
    "synchronization/mutex_unittest.cc",
    "synchronization/thread_annotations_unittest.cc",
    "synchronization/thread_checker_unittest.cc",
    "synchronization/waitable_event_unittest.cc",
    "test/run_all_unittests.cc",
    "time/time_unittest.cc",
  ]

  deps = [
    ":ftl",
    "//third_party/gtest",
  ]
}
